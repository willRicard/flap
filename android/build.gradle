apply plugin: 'com.android.application'

buildscript {
  repositories {
    jcenter()
    google()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.6.0'
  }
}

android {
  compileSdkVersion 29
  buildToolsVersion "30-rc1"
  defaultConfig {
    applicationId 'eu.willricard.flap'
    minSdkVersion 24
    targetSdkVersion 29
    versionCode 1
    versionName '1.0'
    vectorDrawables.useSupportLibrary = true
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt')
    }
    debug {
      applicationIdSuffix '.debug'
      debuggable true
      sourceSets {
        debug {
          // Vulkan validation layers
          jniLibs.srcDir file(android.ndkDirectory.toString() + '/sources/third_party/vulkan/src/build-android/jniLibs')
        }
      }
    }
  }
  flavorDimensions 'versions'
  productFlavors {
    vk {
      applicationIdSuffix '.vk'
    }
    gl {
      applicationIdSuffix '.gl'
      externalNativeBuild {
        cmake {
          arguments "-DFLAP_USE_OPENGL=ON"
        }
      }
    }
  }
  externalNativeBuild {
    cmake {
      version '3.10.2'
      path '../CMakeLists.txt'
    }
  }
  sourceSets {
    main {
      manifest {
        srcFile('AndroidManifest.xml')
      }
      assets.srcDirs = ['../assets']
    }
  }
}

repositories {
  jcenter()
  google()
}
